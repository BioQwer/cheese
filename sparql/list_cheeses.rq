PREFIX rdf:       <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs:      <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cheese:    <http://kasabi.com/dataset/cheese/>
PREFIX geo:       <http://www.w3.org/2003/01/geo/wgs84_pos#>

SELECT ( str(?name) AS ?cheese ) ( str(?cert) AS ?certification ) ( str(?milk_source_label) AS ?source_of_milk ) ( str(?milk_type_label) AS ?type_of_milk ) ?region ?lat ?long
{ 
    ?s rdf:type cheese:Cheese . 
    ?s rdfs:label ?name . 
    ?s cheese:certification ?c . 
    ?c rdfs:label ?cert
    FILTER langmatches( lang(?name), "en") .
    FILTER langmatches( lang(?cert), "en") .

    OPTIONAL {
        ?s cheese:milk_source ?milk_source .
        ?milk_source rdfs:label ?milk_source_label .
        FILTER langmatches( lang(?milk_source_label), "en") .
    }

    OPTIONAL {
        ?s cheese:texture ?milk_texture .
        ?milk_texture rdfs:label ?milk_texture_label .
        FILTER langmatches( lang(?milk_texture_label), "en") .
    }

    OPTIONAL {
        ?s cheese:milk_type ?milk_type .
        ?milk_type rdfs:label ?milk_type_label .
        FILTER langmatches( lang(?milk_type_label), "en") .
    }

    OPTIONAL {
        ?s cheese:region ?region .
    }

    OPTIONAL {
        ?s geo:lat ?lat .
        ?s geo:long ?long .
    }

}
ORDER BY ?name 
