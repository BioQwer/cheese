PREFIX cheese:           <http://data.kasabi.com/dataset/cheese/schema/>
PREFIX rdf:              <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs:             <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:              <http://www.w3.org/2002/07/owl#>
PREFIX xsd:              <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf:             <http://xmlns.com/foaf/0.1/>
PREFIX skos:             <http://www.w3.org/2004/02/skos/core#>
PREFIX cc:               <http://creativecommons.org/ns#>
PREFIX dcterms:          <http://purl.org/dc/terms/>
PREFIX wgs84_pos:        <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX geo:              <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX geonames:         <http://www.geonames.org/ontology#>
PREFIX eurostat:         <http://ec.europa.eu/eurostat/ramon/ontologies/geographic.rdf#>
PREFIX nuts:             <http://ec.europa.eu/eurostat/ramon/rdfdata/nuts2008/>
PREFIX europe:           <http://data.kasabi.com/dataset/europe/>
PREFIX italy:            <http://data.kasabi.com/dataset/italy/>
PREFIX java:             <java:com.kasabi.labs.arq.>
PREFIX italy-schema:     <http://data.kasabi.com/dataset/italy/schema/>


SELECT * {
  ?s a cheese:Cheese .
  ?s skos:prefLabel ?label .
  FILTER langmatches( lang(?label), "en") .
  BIND ( str(?label) AS ?name ) . 
  BIND ( ?s AS ?link )

  OPTIONAL {
    ?s cheese:country ?co .
    ?co rdfs:label ?coul .
    ?co geo:lat ?cola .
    ?co geo:long ?colo .
    BIND ( str(?coul) AS ?country ) 
    BIND ( str (?cola) AS ?country_lat )
    BIND ( str (?colo) AS ?country_long )
  } 

  OPTIONAL {
    ?s cheese:certification ?c .
    ?c rdfs:label ?cl .
    FILTER langmatches( lang(?cl), "en") .
    BIND ( str(?cl) AS ?certification ) 
  }

  OPTIONAL {
    ?s cheese:milkSource ?ms .
    ?ms rdfs:label ?msl .
    FILTER langmatches( lang(?msl), "en") .
    BIND ( str(?msl) AS ?milk )
  }
  
  OPTIONAL {
    ?s cheese:milkType ?milk_type_obj .
    ?milk_type_obj rdfs:label ?milk_type_label .
    FILTER langmatches( lang(?milk_type_label), "en-UK") .
    BIND ( str(?milk_type_label) AS ?milk_type )
  }

  OPTIONAL {
    ?s cheese:use ?cheese_use_obj .
    ?cheese_use_obj rdfs:label ?cheese_use_label .
    FILTER langmatches( lang(?cheese_use_label), "en") .
    BIND ( str(?cheese_use_label) AS ?cheese_use )
  }

  OPTIONAL {
    ?s cheese:milkProcessing ?mp .
    ?mp rdfs:label ?mpl .
    FILTER langmatches( lang(?mpl), "en") .
    BIND ( str(?mpl) AS ?pasteurised )
  }
  
  OPTIONAL {
    ?s cheese:texture ?t .
    ?t rdfs:label ?tl .
    FILTER langmatches( lang(?tl), "en") .
    BIND ( str(?tl) AS ?texture )
  }

  OPTIONAL {
    ?s cheese:use ?u .
    ?u rdfs:label ?use .
  }
  
  OPTIONAL {
    ?s cheese:region ?r .
    ?r rdfs:label ?rl .
    ?r geo:lat ?rla .
    ?r geo:long ?rlo .
    FILTER langmatches( lang(?rl), "it") .
    BIND ( str (?rla) AS ?region_lat )
    BIND ( str (?rlo) AS ?region_long )
    BIND ( str (?rl) AS ?region )
  }

  OPTIONAL {
    ?s cheese:province ?p .
    ?p rdfs:label ?pl .
    ?p geo:lat ?pla .
    ?p geo:long ?plo .
    FILTER langmatches( lang(?pl), "it") .
    BIND ( str (?pla) AS ?province_lat )
    BIND ( str (?plo) AS ?province_long )
    BIND ( str (?pl) AS ?province )
  }
}